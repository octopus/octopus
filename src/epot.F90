!! Copyright (C) 2002 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$

#include "global.h"

module external_pot_m
  use global_m
  use messages_m
  use units_m
  use lib_oct_m
  use lib_oct_parser_m
  use lib_oct_gsl_spline_m
  use datasets_m
  use fft_m
  use math_m
  use mesh_m
  use mesh_function_m
  use grid_m
  use simul_box_m
  use functions_m
#ifdef HAVE_FFT
  use cube_function_m
#endif
  use logrid_m
  use ps_m
  use specie_m
  use specie_grid_m
  use geometry_m
  use states_m
  use lasers_m
  use profiling_m
  use mpi_m
  use mpi_debug_m
  use varinfo_m
  use poisson_m

  implicit none

  private
  public ::                 &
    epot_t,                 &
    epot_init,              &
    epot_end,               &
    epot_generate,          &
    epot_laser_scalar_pot,  &
    epot_laser_field,       &
    epot_laser_vector_pot,  &
    epot_forces,            &
    dproject, zproject,     &
    projector_t

  ! /* The projector data type is intended to hold the non-local part of the
  ! pseudopotentials. The definition of the action of a projector (which is
  ! done through the X(project) subroutine) is:
  ! \hat{P} |phi> = \sum_{ij} uvu(i, j) | ket_i >< bra_j |phi>
  ! i and j run from one to c. The phases are applied whenever the system is
  ! periodic in some dimension.
  ! For the pseudopotentials themselves, a = b. But to calculate the forces,
  ! one needs to put in b the gradient of a. And in the case of the spin-orbit
  ! coupling term, I have put in b the angular momentum of a. */
  type projector_t
    integer          :: n_points_in_sphere
    integer          :: n_channels
    integer, pointer :: jxyz(:)
    FLOAT,   pointer :: uvu(:, :)
    FLOAT,   pointer :: ket(:, :), bra(:, :)
    CMPLX,   pointer :: phases(:, :)
    integer          :: iatom
  end type projector_t

  type epot_t
    ! Classic charges:
    integer :: classic_pot        ! How to include the classic charges
    FLOAT, pointer :: vclassic(:) ! We use it to store the potential of the classic charges

    ! Ions
    FLOAT,     pointer :: vpsl(:)          ! the local part of the pseudopotentials
#ifdef HAVE_FFT
    type(dcf_t), pointer :: local_cf(:)      ! for the local pseudopotential in Fourier space
    type(dcf_t), pointer :: rhocore_cf(:)    ! and for the core density
#endif

    integer :: nvnl                        ! number of nonlocal operators
    type(projector_t), pointer :: p(:)
    type(projector_t), pointer :: dp(:, :)
    type(projector_t), pointer :: lso(:, :)

    ! External e-m fields
    integer :: no_lasers                   ! number of laser pulses used
    type(laser_t), pointer :: lasers(:) ! lasers stuff
    FLOAT, pointer :: E_field(:)           ! static electric field
    FLOAT, pointer :: v_static(:)          ! static scalar potential
    FLOAT, pointer :: B_field(:)           ! static magnetic field
    FLOAT, pointer :: A_static(:,:)        ! static vector potential

    ! The gyromagnetic ratio (-2.0 for the electron, but different if we treat
    ! *effective* electrons in a quantum dot. It affects the spin Zeeman term.
    FLOAT :: gyromagnetic_ratio

  end type epot_t

contains

  ! ---------------------------------------------------------
  subroutine epot_init(ep, gr)
    type(epot_t), intent(out) :: ep
    type(grid_t), intent(in)  :: gr

    integer :: i, j
    integer(POINTER_SIZE) :: blk
    FLOAT, allocatable :: x(:)

    call push_sub('epot.epot_init')

    ! Local part of the pseudopotentials
    ALLOCATE(ep%vpsl(NP), NP)
    ep%vpsl = M_ZERO

    ! should we calculate the local pseudopotentials in Fourier space?
    ! This depends on wether we have periodic dimensions or not
    if(simul_box_is_periodic(gr%sb).and.(.not.gr%geo%only_user_def)) then
      call epot_local_fourier_init(ep, gr%m, gr%sb, gr%geo)
    end if

    ep%classic_pot = 0
    if(gr%geo%ncatoms > 0) then

      !%Variable ClassicPotential
      !%Type integer
      !%Default 0
      !%Section Hamiltonian
      !%Description
      !% If <tt>true</tt>, add to the external potential the potential generated by 
      !% the point charges read from the PDB input (see <tt>PBDCoordinates</tt>).
      !%End
      call loct_parse_int(check_inp('ClassicPotential'), 0, ep%classic_pot)
      if(ep%classic_pot > 0) then
        message(1) = 'Info: generating classic external potential'
        call write_info(1)

        ALLOCATE(ep%Vclassic(NP), NP)
        call epot_generate_classic(ep, gr%m, gr%geo)
      end if
    end if

    ! lasers
    call laser_init(ep%no_lasers, ep%lasers, gr%m)
    if(ep%no_lasers>0.and.mpi_grp_is_root(mpi_world)) then
      message(1) = 'Info: Lasers'
      call write_info(1)

      call laser_write_info(ep%no_lasers, ep%lasers, stdout)
    end if

    !%Variable StaticElectricField
    !%Type block
    !%Section Hamiltonian
    !%Description
    !% A static constant electrical field may be added to the usual Hamiltonian,
    !% by setting the block StaticElectricField. Atomic units will be assumed
    !% always for its magnitude, regardless of the unit system specified.
    !% The three possible components of the block (which should only have one
    !% line) are the three components of the electrical field vector.
    !%End
    nullify(ep%E_field, ep%v_static)
    if(loct_parse_block(check_inp('StaticElectricField'), blk)==0) then
      ALLOCATE(ep%E_field(NDIM), NDIM)
      do i = 1, NDIM
        call loct_parse_block_float(blk, 0, i-1, ep%E_field(i))
      end do
      call loct_parse_block_end(blk)
      
      ! Compute the scalar potential
      ALLOCATE(ep%v_static(NP), NP)
      do i = 1, NP
        ep%v_static(i) = sum(gr%m%x(i,:)*ep%E_field(:))
      end do
    end if

    !%Variable StaticMagneticField
    !%Type block
    !%Section Hamiltonian
    !%Description
    !% A static constant magnetic field may be added to the usual Hamiltonian,
    !% by setting the block StaticMagneticField. Atomic units will be assumed
    !% always for its magnitude, regardless of the unit system specified.
    !% The three possible components of the block (which should only have one
    !% line) are the three components of the magnetic field vector. Note that
    !% if you are running the code in 1D mode this will not work, and if you
    !% are running the code in 2D mode the magnetic field will have to be in
    !% the z-direction, so that the first two columns should be zero.
    !%End
    nullify(ep%B_field, ep%A_static)
    if(loct_parse_block(check_inp('StaticMagneticField'), blk)==0) then

      ALLOCATE(ep%B_field(3), 3)
      do i = 1, 3
        call loct_parse_block_float(blk, 0, i-1, ep%B_field(i))
      end do
      select case(calc_dim)
      case(1)
        call input_error('StaticMagneticField')
      case(2)
        if(ep%B_field(1)**2+ep%B_field(2)**2 > M_ZERO) call input_error('StaticMagneticField')
      end select
      call loct_parse_block_end(blk)

      ! Compute the vector potential
      ALLOCATE(ep%A_static(NP, NDIM), NP*NDIM)
      ALLOCATE(x(NDIM), NDIM)
      do i = 1, NP
        x(1:NDIM) = gr%m%x(i, 1:NDIM)
        select case (NDIM)
        case (2)
          ep%A_static(i, :) = (/x(2), -x(1)/)*ep%B_field(3)
        case (3)
          ep%A_static(i, :) = (/x(2)*ep%B_field(3) - x(3)*ep%B_field(2), &
            x(3)*ep%B_field(1) - x(1)*ep%B_field(3), x(1)*ep%B_field(2) - x(2)*ep%B_field(1)/)
        end select
      end do
      deallocate(x)
      ep%A_static = -M_HALF/P_c*ep%A_static

    end if

    !%Variable GyromagneticRatio
    !%Type float
    !%Default 2.0023193043768
    !%Section Hamiltonian
    !%Description
    !% The gyromagnetic ratio of the electron. This is of course a physical 
    !% constant, and the default value is the exact one that you should not 
    !% touch, unless : 
    !% 
    !% (i)  You want to disconnect the anomalous Zeeman term in the Hamiltonian 
    !% (then set it to zero, this number only affects this term);
    !% 
    !% (ii) You are using an effective Hamiltonian, as it is the case when
    !% you calculate a 2D electron gas, in which case you have an effective
    !% gyromagnetic factor that dependes on the material.
    !%End
    call loct_parse_float(check_inp('GyromagneticRatio'), P_g, ep%gyromagnetic_ratio)

    ! The projectors
    ep%nvnl = geometry_nvnl(gr%geo)
    nullify(ep%p)

    nullify(ep%dp)
    nullify(ep%lso)
    
    if(ep%nvnl > 0) then
      ALLOCATE(ep%p(ep%nvnl), ep%nvnl)
      do i = 1, ep%nvnl
        nullify(ep%p(i)%jxyz, ep%p(i)%bra, ep%p(i)%ket, ep%p(i)%uvu, ep%p(i)%phases)
      end do
      ALLOCATE(ep%dp(NDIM, ep%nvnl), NDIM*ep%nvnl)
      do i = 1, ep%nvnl
        do j = 1, NDIM
          nullify(ep%dp(j, i)%jxyz, ep%dp(j, i)%bra, ep%dp(j, i)%ket, ep%dp(j, i)%uvu, ep%dp(j, i)%phases)
        end do
      end do
      ALLOCATE(ep%lso(NDIM, ep%nvnl), NDIM*ep%nvnl)
      do i = 1, ep%nvnl
        do j = 1, NDIM
          nullify(ep%lso(j, i)%jxyz, ep%lso(j, i)%bra, ep%lso(j, i)%ket, ep%lso(j, i)%uvu, ep%lso(j, i)%phases)
        end do
      end do
    end if

    call pop_sub()
  end subroutine epot_init


  ! ---------------------------------------------------------
  subroutine epot_end(ep, sb, geo)
    type(epot_t),      intent(inout) :: ep
    type(simul_box_t), intent(in)    :: sb
    type(geometry_t),  intent(in)    :: geo


#ifdef HAVE_FFT
    integer :: i

    if(simul_box_is_periodic(sb).and.(.not.geo%only_user_def)) then
      do i = 1, geo%nspecies
        call dcf_free(ep%local_cf(i))
        if(geo%specie(i)%nlcc) call dcf_free(ep%rhocore_cf(i))
      end do
      deallocate(ep%local_cf)
      if(geo%nlcc) deallocate(ep%rhocore_cf)
    end if

#endif

    if(associated(ep%vpsl)) then
      deallocate(ep%vpsl)
      nullify(ep%vpsl)
    end if

    if(ep%classic_pot > 0) then
      ep%classic_pot = 0
      ASSERT(associated(ep%Vclassic)) ! sanity check
      deallocate(ep%Vclassic)         ! and clean up
      nullify(ep%Vclassic)
    end if

    ! the external laser
    call laser_end(ep%no_lasers, ep%lasers)

    ! the macroscopic fields
    if(associated(ep%E_field))  deallocate(ep%E_field)
    if(associated(ep%v_static)) deallocate(ep%v_static)
    if(associated(ep%B_field))  deallocate(ep%B_field)
    if(associated(ep%A_static)) deallocate(ep%A_static)

    if(ep%nvnl>0) then
      ASSERT(associated(ep%p))
      deallocate(ep%p); nullify(ep%p)

      ASSERT(associated(ep%dp))
      deallocate(ep%dp); nullify(ep%dp)

      ! Here the spin-orbit should be finalized, but only if they have been built...
    end if

  end subroutine epot_end


#ifdef HAVE_FFT
  ! ---------------------------------------------------------
  subroutine epot_local_fourier_init(ep, m, sb, geo)
    type(epot_t),      intent(inout) :: ep
    type(mesh_t),      intent(in)    :: m
    type(simul_box_t), intent(in)    :: sb
    type(geometry_t),  intent(in)    :: geo

    integer :: vlocal_cutoff
    integer :: i, ix, iy, iz, ixx(MAX_DIM), db(MAX_DIM), c(MAX_DIM)
    FLOAT :: x(MAX_DIM)
    FLOAT :: gpar, gperp, gx, gz, modg
    FLOAT :: r_0, temp(MAX_DIM), tmp, norm

    type(specie_t), pointer :: s ! shortcuts
    type(dcf_t), pointer :: cf

    call push_sub('epot.epot_local_fourier_init')

    !%Variable VlocalCutoff
    !%Type integer
    !%Default value of PeriodicDimensions
    !%Section Hamiltonian
    !%Description
    !% Define which cutoff type is to be applied to the long range part of the local potential
    !% A cutoff is used when one wants to avoid the long range interactions
    !% among the system enclosed in the simulation box and (some of) its periodic images
    !%Option cutoff_sphere 0
    !% Cut off the interaction out of a sphere
    !%Option cutoff_cylinder 1
    !% Cut off the interaction out of a cylinder with axis parallel to the x direction
    !%Option cutoff_slab 2
    !% Cut off the interaction out of a slab in the xy plane
    !%Option cutoff_none 3
    !% Do not apply any cutoff: all the periodic images interact
    !%End
    call loct_parse_int(check_inp('VlocalCutoff'), sb%periodic_dim , vlocal_cutoff)
    if(.not.varinfo_valid_option('VlocalCutoff', vlocal_cutoff)) call input_error('VlocalCutoff')
    call messages_print_var_option(stdout, "VlocalCutoff", vlocal_cutoff)

    if (vlocal_cutoff /= sb%periodic_dim) then
      write(message(1), '(a,i1,a)')'The System is periodic in ', sb%periodic_dim, ' dimension(s),'
      write(message(2), '(a,i1,a)')'but VlocalCutoff is set for ', vlocal_cutoff, ' dimensions.'
      call write_warning(2)
    end if

    ALLOCATE(ep%local_cf(geo%nspecies), geo%nspecies)
    if(geo%nlcc) ALLOCATE(ep%rhocore_cf(geo%nspecies), geo%nspecies)

    specie: do i = 1, geo%nspecies
      s  => geo%specie(i)
      cf => ep%local_cf(i)

      !%Variable VlocalCutoffRadius
      !%Type float
      !%Default value of the largest nonperiodic box length
      !%Section Hamiltonian
      !%Description
      !% The maximum length out of which the long range part of the interaction
      !% is cut off. 
      !% It refers to the radius of the cylinder if VlocalCutoff = 1,
      !% to the thickness od the slab if VlocalCutoff = 2.
      !%End

      if(i == 1) then
        call mesh_double_box(sb, m, db)
        call dcf_new(db, cf)    ! initialize the cube
        call dcf_fft_init(cf, sb)   ! and initialize the ffts
        db = cf%n               ! dimensions of the box may have been optimized, so get them
        c(:) = db(:)/2 + 1      ! get center of double box
        if (vlocal_cutoff == 3) then
          r_0 = M_ZERO
        else
          call loct_parse_float(check_inp('VlocalCutoffRadius'),&
            maxval(db(:)*m%h(:)/M_TWO)/units_inp%length%factor , r_0)
          r_0 = r_0*units_inp%length%factor
          write(message(1),'(3a,f12.6)')'Info: Vlocal Cutoff Radius [',  &
            trim(units_out%length%abbrev), '] = ',       &
            r_0/units_out%length%factor
          call write_info(1)
        end if
      else
        call dcf_new_from(cf, ep%local_cf(1))   ! we can just copy from the first one
      end if

      if(geo%nlcc) call dcf_new_from(ep%rhocore_cf(i), ep%local_cf(1))

      call dcf_alloc_FS(cf)      ! allocate the tube in Fourier space

      norm    = M_FOUR*M_PI/m%vol_pp(1)
      temp(:) = M_TWO*M_PI/(db(:)*m%h(:))
      cf%FS   = M_Z0
      do ix = 1, cf%nx
        ixx(1) = pad_feq(ix, db(1), .true.)
        do iy = 1, db(2)
          ixx(2) = pad_feq(iy, db(2), .true.)
          do iz = 1, db(3)
            ixx(3) = pad_feq(iz, db(3), .true.)

            x = temp(:)*ixx(:)
            modg = sqrt(sum((temp(:)*ixx(:))**2))

            tmp = specie_get_local_fourier(sb%dim, s, x)
            if(modg /= M_ZERO) then
              tmp = tmp - s%z_val*exp(-(modg/(2*s%ps%a_erf))**2)/modg**2
              select case(vlocal_cutoff)
              case(0)
                cf%FS(ix, iy, iz) = tmp*cutoff0(modg, r_0)
              case(1)
                gx = abs(temp(1)*ixx(1))
                gperp = sqrt((temp(2)*ixx(2))**2 + (temp(3)*ixx(3))**2)
                cf%FS(ix, iy, iz) = tmp*cutoff1(gx, gperp, r_0)
              case(2)
                gz = abs(temp(3)*ixx(3))
                gpar = sqrt((temp(1)*ixx(1))**2 + (temp(2)*ixx(2))**2)
                cf%FS(ix, iy, iz) = tmp*cutoff2(gpar, gz, r_0)
              case(3)
                cf%FS(ix, iy, iz) = tmp
              end select
            else
              select case(vlocal_cutoff)
              case(0)  ; cf%FS(ix, iy, iz) = -r_0**2/M_TWO
              case(1,2); cf%FS(ix, iy, iz) = M_ZERO
              case(3)  ; cf%FS(ix, iy, iz) = tmp
              end select
            end if

            ! multiply by normalization factor and a phase shift to get the center of the box
            ! the phase is exp(-i kR), where R denotes the center of the box
            ! note that c is a fortran index that starts at 1
            cf%FS(ix, iy, iz) = norm*        &
               exp(-M_ZI*sum(x(:)*(c(:)-1)*m%h(:)))*   &
               cf%FS(ix, iy, iz)
          end do
        end do
      end do

      ! now we built the non-local core corrections in momentum space
      nlcc: if(s%nlcc) then
        call dcf_alloc_RS(ep%rhocore_cf(i))

        do ix = 1, db(1)
          ixx(1) = ix - c(1)
          do iy = 1, db(2)
            ixx(2) = iy - c(2)
            do iz = 1, db(3)
              ixx(3) = iz - c(3)

              x(:) = m%h(:)*ixx(:)
              ep%rhocore_cf(i)%RS(ix, iy, iz) = specie_get_nlcc(s, x)
            end do
          end do
        end do
        call dcf_alloc_FS(ep%rhocore_cf(i))      ! allocate the tube in Fourier space
        call dcf_RS2FS(ep%rhocore_cf(i))         ! Fourier transform
        call dcf_free_RS(ep%rhocore_cf(i))       ! we do not need the real space any longer
      end if nlcc

    end do specie

    call pop_sub()
  end subroutine epot_local_fourier_init
#endif


  ! ---------------------------------------------------------
  subroutine kill_projector(p)
    type(projector_t), intent(inout) :: p
    integer :: ierr
    if(associated(p%jxyz)) then
       deallocate(p%jxyz, stat = ierr)
    end if
    nullify(p%jxyz)
    if(associated(p%uvu)) then
       deallocate(p%uvu, stat = ierr)
    end if
    nullify(p%uvu)
    if(associated(p%bra)) then
       deallocate(p%bra, stat = ierr)
    end if
    nullify(p%bra)
    if(associated(p%ket)) then
       deallocate(p%ket, stat = ierr)
    end if
    nullify(p%ket)
    if(associated(p%phases)) then
       deallocate(p%phases, stat = ierr)
    end if
    nullify(p%phases)
  end subroutine kill_projector


  ! ---------------------------------------------------------
  subroutine epot_generate(ep, gr, st, reltype, fast_generation)
    type(epot_t),      intent(inout) :: ep
    type(grid_t), target,  intent(inout) :: gr
    type(states_t),    intent(inout) :: st
    integer,           intent(in)    :: reltype
    logical, optional, intent(in)    :: fast_generation

    logical :: fast_generation_
    integer :: ia, i, l, lm, k, p, j
    type(specie_t), pointer :: s
    type(atom_t),   pointer :: a
    type(dcf_t) :: cf_loc, cf_nlcc
    type(mesh_t),      pointer :: m
    type(simul_box_t), pointer :: sb
    type(geometry_t),  pointer :: geo


    call profiling_in(C_PROFILING_EPOT_GENERATE)
    call push_sub('epot.epot_generate')

    sb  => gr%sb
    geo => gr%geo
    m   => gr%m

    fast_generation_ = .false.
    if (present(fast_generation)) fast_generation_ = fast_generation

    ! first we assume that we need to recalculate the ion_ion energy
    geo%eii = ion_ion_energy(geo)

#ifdef HAVE_FFT
    if(simul_box_is_periodic(sb).and.(.not.geo%only_user_def)) then
      call dcf_new_from(cf_loc, ep%local_cf(1)) ! at least one specie must exist
      call dcf_alloc_FS(cf_loc)
      cf_loc%FS = M_z0

      if(geo%nlcc) then
        call dcf_new_from(cf_nlcc, ep%local_cf(1)) ! at least one specie must exist
        call dcf_alloc_FS(cf_nlcc)
        cf_nlcc%FS = M_z0
      end if
    end if
#endif

    ! Local.
    ep%vpsl = M_ZERO
    do ia = 1, geo%natoms
      a => geo%atom(ia) ! shortcuts
      s => a%spec
      call build_local_part()
    end do

    ! Nonlocal part.
    i = 1
    do ia = 1, geo%natoms
      a => geo%atom(ia)
      s => a%spec
      if(s%local) cycle
      p = 1

      do l = 0, s%ps%l_max
        if(s%ps%l_loc == l) cycle
        do lm = -l, l

          if(.not.fast_generation_) then

            ! This if is a performance hack, necessary for when the ions move.
            ! For each atom, the sphere is the same, so we just calculate it once.
            if(p == 1) then

              k = i
              p = 2
              call kill_projector(ep%p(i))
              do j = 1, sb%dim
                call kill_projector(ep%dp(j, i))
                call kill_projector(ep%lso(j, i))
              end do
              call build_kb_sphere(i)

            else

              call kill_projector(ep%p(i))
              ep%p(i)%n_points_in_sphere = ep%p(k)%n_points_in_sphere
              ep%p(i)%n_channels = ep%p(k)%n_channels
              ALLOCATE(ep%p(i)%jxyz(ep%p(k)%n_points_in_sphere), ep%p(k)%n_points_in_sphere)
              ep%p(i)%jxyz = ep%p(k)%jxyz
              do j = 1, sb%dim
                call kill_projector(ep%dp(j, i))
                ep%dp(j, i)%n_points_in_sphere = ep%dp(j, k)%n_points_in_sphere
                ep%dp(j, i)%n_channels = ep%dp(j, k)%n_channels
                ALLOCATE(ep%dp(j, i)%jxyz(ep%dp(j, k)%n_points_in_sphere), ep%dp(j, k)%n_points_in_sphere)
                ep%dp(j, i)%jxyz = ep%dp(j, k)%jxyz
              end do
              do j = 1, sb%dim
                call kill_projector(ep%lso(j, i))
                ep%lso(j, i)%n_points_in_sphere = ep%lso(j, k)%n_points_in_sphere
                ep%lso(j, i)%n_channels = ep%lso(j, k)%n_channels
                ALLOCATE(ep%lso(j, i)%jxyz(ep%lso(j, k)%n_points_in_sphere), ep%lso(j, k)%n_points_in_sphere)
                ep%lso(j, i)%jxyz = ep%lso(j, k)%jxyz
              end do

            end if

            call allocate_nl_part(i)

          end if

          call build_nl_part(i, l, lm)

          ep%p(i)%iatom             = ia
          ep%dp(1:sb%dim, i)%iatom  = ia
          ep%lso(1:sb%dim, i)%iatom = ia

          i = i + 1
        end do
      end do
    end do

#ifdef HAVE_FFT
    if(simul_box_is_periodic(sb).and.(.not.geo%only_user_def)) then
      ! first the potential
      call dcf_alloc_RS(cf_loc)
      call dcf_FS2RS(cf_loc)
      call dcf2mf(m, cf_loc, ep%vpsl)
      call dcf_free(cf_loc)

      ! and the non-local core corrections
      if(geo%nlcc) then
        call dcf_alloc_RS(cf_nlcc)
        call dcf_FS2RS(cf_nlcc)
        call dcf2mf(m, cf_nlcc, st%rho_core)
        call dcf_free(cf_nlcc)
      end if
    end if
#endif

    if (ep%classic_pot > 0) then
      ep%vpsl(1:m%np) = ep%vpsl(1:m%np) + ep%vclassic(1:m%np)
    end if

    call pop_sub()
    call profiling_out(C_PROFILING_EPOT_GENERATE)

  contains

    ! ---------------------------------------------------------
    subroutine build_local_part()
      integer :: i
      FLOAT :: x(MAX_DIM)
      FLOAT, allocatable  :: rho(:), phi(:)

      call push_sub('epot.build_local_part')

      if((.not.simul_box_is_periodic(sb)).or.geo%only_user_def) then
        do i = 1, m%np
          x(:) = m%x(i, :) - a%x(:)
          ep%vpsl(i) = ep%vpsl(i) + specie_get_local(s, x)
          if(s%nlcc) then
            st%rho_core(i) = st%rho_core(i) + specie_get_nlcc(s, x)
          end if

        end do

        if(s%has_density) then 
          ALLOCATE(rho(1:m%np),m%np)
          ALLOCATE(phi(1:m%np_part),m%np_part)

          call specie_get_density(s,a%x,m,rho)
          call dpoisson_solve(gr,phi,rho)
          ep%vpsl(1:m%np)=ep%vpsl(1:m%np)+phi(1:m%np)

          deallocate(rho)
          deallocate(phi)
        end if

#ifdef HAVE_FFT
      else ! momentum space
        call cf_phase_factor(sb, m, a%x, ep%local_cf(s%index), cf_loc)
        if(s%nlcc) then
          call cf_phase_factor(sb, m, a%x, ep%rhocore_cf(s%index), cf_nlcc)
        end if
#endif
      end if

      call pop_sub()
    end subroutine build_local_part


    ! ---------------------------------------------------------
    subroutine build_kb_sphere(ivnl)
      integer, intent(in) :: ivnl
      integer :: i, j, k, d
      FLOAT :: r

      call push_sub('epot.build_kb_sphere')

      if (any(s%ps%rc_max + m%h(1) >= sb%lsize(1:sb%periodic_dim))) then
        message(1)='KB sphere is larger than the box size'
        write(message(2),'(a,f12.6,a)')  '  rc_max+h = ', s%ps%rc_max + m%h(1), ' [b]'
        write(message(3),'(a,3f12.4,a)') '  lsize    = ', sb%lsize, ' [b]'
        message(4)='Please change pseudopotential'
        call write_fatal(4)
      end if
      j = 0
      do k = 1, m%np
        do i = 1, 3**sb%periodic_dim
          call mesh_r(m, k, r, a=a%x + sb%shift(i,:))
          if(r > s%ps%rc_max + m%h(1)) cycle
          j = j + 1
          exit
        end do
      end do

      ep%p(ivnl)%n_points_in_sphere = j
      ep%p(ivnl)%n_channels         = s%ps%kbc

      do d = 1, sb%dim
        ep%dp(d, ivnl)%n_points_in_sphere = j
        ep%dp(d, ivnl)%n_channels         = s%ps%kbc
      end do

      do d = 1, sb%dim
        ep%lso(d, ivnl)%n_points_in_sphere = j
        ep%lso(d, ivnl)%n_channels         = s%ps%kbc
      end do

      ALLOCATE(ep%p(ivnl)%jxyz(j), j)
      do d = 1, sb%dim
        ALLOCATE(ep%dp(d, ivnl)%jxyz(j), j)
      end do
      do d = 1, sb%dim
        ALLOCATE(ep%lso(d, ivnl)%jxyz(j), j)
      end do

      j = 0
      do k = 1, m%np
        do i = 1, 3**sb%periodic_dim
          call mesh_r(m, k, r, a=a%x + sb%shift(i,:))
          ! we enlarge slightly the mesh (good for the interpolation scheme)
          if(r > s%ps%rc_max + m%h(1)) cycle
          j = j + 1
          ep%p(ivnl)%jxyz(j) = k
          do d = 1, sb%dim
            ep%dp(d, ivnl)%jxyz(j) = k
          end do
          do d = 1, sb%dim
            ep%lso(d, ivnl)%jxyz(j) = k
          end do
          exit
        end do
      end do

      call pop_sub()
    end subroutine build_kb_sphere


    ! ---------------------------------------------------------
    subroutine allocate_nl_part(ivnl)
      integer, intent(in) :: ivnl
      integer :: n_s, n_c, d
      call push_sub('epot.allocate_nl_part')

      n_s = ep%p(ivnl)%n_points_in_sphere
      n_c = ep%p(ivnl)%n_channels

      ALLOCATE(ep%p(ivnl)%ket(n_s, n_c), n_s*n_c)
      ALLOCATE(ep%p(ivnl)%bra(n_s, n_c), n_s*n_c)
      ALLOCATE(ep%p(ivnl)%uvu(n_c, n_c), n_c*n_c)
      ep%p(ivnl)%ket(:,:) =  M_ZERO
      ep%p(ivnl)%bra(:,:) =  M_ZERO
      ep%p(ivnl)%uvu(:,:) =  M_ZERO

      do d = 1, sb%dim
        ALLOCATE(ep%dp(d, ivnl)%ket(n_s, n_c), n_s*n_c)
        ALLOCATE(ep%dp(d, ivnl)%bra(n_s, n_c), n_s*n_c)
        ALLOCATE(ep%dp(d, ivnl)%uvu(n_c, n_c), n_c*n_c)
        ep%dp(d, ivnl)%ket(:,:) = M_ZERO
        ep%dp(d, ivnl)%bra(:,:) = M_ZERO
        ep%dp(d, ivnl)%uvu(:,:) = M_ZERO
      end do

      do d = 1, sb%dim
        ALLOCATE(ep%lso(d, ivnl)%ket(n_s, n_c), n_s*n_c)
        ALLOCATE(ep%lso(d, ivnl)%bra(n_s, n_c), n_s*n_c)
        ALLOCATE(ep%lso(d, ivnl)%uvu(n_c, n_c), n_c*n_c)
        ep%lso(d, ivnl)%ket(:,:) = M_ZERO
        ep%lso(d, ivnl)%bra(:,:) = M_ZERO
        ep%lso(d, ivnl)%uvu(:,:) = M_ZERO
      end do

      if(sb%periodic_dim/=0) then
        ALLOCATE(ep%p(ivnl)%phases(n_s, st%d%nik), n_s*st%d%nik)
        do d = 1, sb%dim
          ALLOCATE( ep%dp(d,ivnl)%phases(n_s, st%d%nik), n_s*st%d%nik)
          ALLOCATE(ep%lso(d,ivnl)%phases(n_s, st%d%nik), n_s*st%d%nik)
        end do
      end if

      call pop_sub()
    end subroutine allocate_nl_part


    ! ---------------------------------------------------------
    subroutine build_nl_part(ivnl, l, lm)
      integer, intent(in) :: ivnl, l, lm

      integer :: i, j, k, d
      FLOAT :: r, x(MAX_DIM), x_in(MAX_DIM)
      FLOAT :: so_uv, so_duv(MAX_DIM)
      FLOAT :: v, dv(MAX_DIM)

      integer :: n_c, n_s
      CMPLX, allocatable :: grad_so(:, :, :)

      call push_sub('epot.build_nl_part')

      n_s = ep%p(ivnl)%n_points_in_sphere
      n_c = ep%p(ivnl)%n_channels
      ALLOCATE(grad_so(n_s, 3, n_c), n_s*3*n_c)
      grad_so = M_z0

      j_loop: do j = 1, n_s
        x_in(:) = m%x(ep%p(ivnl)%jxyz(j), :)
        k_loop: do k = 1, 3**sb%periodic_dim
          x(:) = x_in(:) - sb%shift(k,:) - a%x
          r = sqrt(sum(x*x))
          if (r > s%ps%rc_max + m%h(1)) cycle

          i_loop : do i = 1, n_c
            if(l .ne. s%ps%L_loc) then
              call specie_get_nl_part(s, x, l, lm, i, v, dv(1:3))
              ep%p(ivnl)%ket(j, i) = v
              ep%p(ivnl)%bra(j, i) = v
              do d = 1, sb%dim
                ep%dp(d, ivnl)%ket(j, i) = dv(d)
                ep%dp(d, ivnl)%bra(j, i) = v
              end do
            end if

            if(l>0 .and. s%ps%so_l_max>=0) then
              call specie_get_nl_part(s, x, l, lm, i, so_uv, so_duv(:), so=.true.)
              grad_so(j, 1:3, i) = so_duv(1:3)
              do d = 1, sb%dim
                ep%lso(d, ivnl)%ket(j, i) = so_uv
              end do
            end if
          end do i_loop

        end do k_loop
      end do j_loop

      if(reltype == 1) then ! SPIN_ORBIT
        do j = 1, n_s
          x_in(1:3) = m%x(ep%lso(1, ivnl)%jxyz(j), 1:3)
          x(1:3) = x_in(1:3) !- sb%shift(k, 1:3) !???
          x = x - a%x
          r = sqrt(sum(x*x))

          ep%lso(1, ivnl)%bra(j, 1:n_c) = (x(2)*grad_so(j, 3, 1:n_c) - x(3)*grad_so(j, 2, 1:n_c))
          ep%lso(2, ivnl)%bra(j, 1:n_c) = (x(3)*grad_so(j, 1, 1:n_c) - x(1)*grad_so(j, 3, 1:n_c))
          ep%lso(3, ivnl)%bra(j, 1:n_c) = (x(1)*grad_so(j, 2, 1:n_c) - x(2)*grad_so(j, 1, 1:n_c))
        end do
      end if

      ! and here we calculate the uVu
      if(s%ps%flavour == PS_TM2) then
        if(l .ne. s%ps%L_loc) then
          ep%p(ivnl)%uvu(1, 1) = s%ps%h(l, 1, 1)
          do d = 1, sb%dim
            ep%dp(d, ivnl)%uvu(1, 1) = s%ps%h(l, 1, 1)
          end do
        end if
      else
        ep%p(ivnl)%uvu(1:3, 1:3) = s%ps%h(l, 1:3, 1:3)
        do d = 1, sb%dim
          ep%dp(d, ivnl)%uvu(1:3, 1:3) = s%ps%h(l, 1:3, 1:3)
        end do
        do d = 1, sb%dim
          ep%lso(d, ivnl)%uvu(1:3, 1:3) = s%ps%k(l, 1:3, 1:3)
        end do
      end if

      if(sb%periodic_dim/=0) then
        do j = 1, n_s
          x(:) = m%x(ep%p(ivnl)%jxyz(j), :)
          do k = 1, st%d%nik
            ep%p(ivnl)%phases(j, k) = exp(M_zI*sum(st%d%kpoints(:, k)*x(:)))
            do d = 1, sb%dim
              ep%dp(d, ivnl)%phases(j, k) = exp(M_zI*sum(st%d%kpoints(:, k)*x(:)))
            end do
            do d = 1, sb%dim
              ep%lso(d, ivnl)%phases(j, k) = exp(M_zI*sum(st%d%kpoints(:, k)*x(:)))
            end do
          end do
        end do
      end if

      call pop_sub()
    end subroutine build_nl_part

  end subroutine epot_generate


  ! ---------------------------------------------------------
  subroutine epot_generate_classic(ep, m, geo)
    type(epot_t),     intent(inout) :: ep
    type(mesh_t),     intent(in)    :: m
    type(geometry_t), intent(in)    :: geo

    integer i, ia
    FLOAT :: r, rc

    call push_sub('epot.epot_generate_classic')

    ep%Vclassic = M_ZERO
    do ia = 1, geo%ncatoms
      do i = 1, m%np
        call mesh_r(m, i, r, a=geo%catom(ia)%x)
        select case(ep%classic_pot)
        case(1) ! point charge
          if(r < r_small) r = r_small
          ep%Vclassic(i) = ep%Vclassic(i) - geo%catom(ia)%charge/r
        case(2) ! gaussion smeared charge
          select case(geo%catom(ia)%label(1:1)) ! covalent radii
          case('H')
            rc = CNST(0.4)*P_Ang
          case('C')
            rc = CNST(0.8)*P_Ang
          case default
            rc = CNST(0.7)*P_Ang
          end select
          if(abs(r - rc) < r_small) r = rc + sign(r_small, r-rc)
          ep%Vclassic(i) = ep%Vclassic(i) - geo%catom(ia)%charge*(r**4 - rc**4)/(r**5 - rc**5)
        end select
      end do
    end do

    call pop_sub()
  end subroutine epot_generate_classic


  ! ---------------------------------------------------------
  function epot_laser_scalar_pot(np, gr, ep, t) result(v)
    integer,      intent(in) :: np
    type(grid_t), intent(in) :: gr
    type(epot_t), intent(in) :: ep
    FLOAT,        intent(in) :: t

    FLOAT :: v(np)

    call laser_potential(gr%sb, ep%no_lasers, ep%lasers, t, gr%m, v)

  end function epot_laser_scalar_pot


  ! ---------------------------------------------------------
  subroutine epot_laser_vector_pot(sb, ep, t, a)
    type(simul_box_t), intent(in)  :: sb
    type(epot_t),      intent(in)  :: ep
    FLOAT,             intent(in)  :: t
    FLOAT,             intent(out) :: a(sb%dim)

    call laser_vector_field(sb, ep%no_lasers, ep%lasers, t, a)

  end subroutine epot_laser_vector_pot


  ! ---------------------------------------------------------
  subroutine epot_laser_field(sb, ep, t, e)
    type(simul_box_t), intent(in)  :: sb
    type(epot_t),      intent(in)  :: ep
    FLOAT,             intent(in)  :: t
    FLOAT,             intent(out) :: e(sb%dim)

    call laser_field(sb, ep%no_lasers, ep%lasers, t, e)

  end subroutine epot_laser_field


  ! ---------------------------------------------------------
  subroutine epot_forces(gr, ep, st, t)
    type(grid_t), target, intent(in) :: gr
    type(epot_t),     intent(in)     :: ep
    type(states_t),   intent(in)     :: st
    FLOAT,     optional, intent(in)    :: t

    type(geometry_t), pointer :: geo
    integer :: i, j, l, idim, ist, ik, ivnl, ivnl_start, ivnl_end
    FLOAT :: d, r, zi, zj, x(MAX_DIM)
    type(atom_t), pointer :: atm

    FLOAT, allocatable :: dppsi(:, :)
    FLOAT :: dz
    CMPLX, allocatable :: zppsi(:, :)
    CMPLX :: zz

#if defined(HAVE_MPI)
    FLOAT :: f(MAX_DIM)
#endif

    call profiling_in(C_PROFILING_FORCES)
    call push_sub('epot.epot_forces')

    geo => gr%geo

    ! init to 0
    do i = 1, geo%natoms
      geo%atom(i)%f = M_ZERO
    end do

    if(.not.geo%only_user_def) then
      ! non-local component of the potential.
      if (st%d%wfs_type == M_REAL) then
        ALLOCATE(dppsi(gr%m%np, st%d%dim), gr%m%np*st%d%dim)
      else
        ALLOCATE(zppsi(gr%m%np, st%d%dim), gr%m%np*st%d%dim)
      end if

      atm_loop: do i = 1, geo%natoms
        atm => geo%atom(i)
        if(atm%spec%local) cycle

        ! Here we learn which are the projector that correspond to atom i.
        ! It assumes that the projectors of each atom are consecutive.
        ivnl_start  = - 1
        do ivnl = 1, ep%nvnl
          if(ep%dp(1, ivnl)%iatom .eq. i) then
            ivnl_start = ivnl
            exit
          end if
        end do
        if(ivnl_start .eq. -1) cycle
        ivnl_end = ep%nvnl
        do ivnl = ivnl_start, ep%nvnl
          if(ep%dp(1, ivnl)%iatom .ne. i) then
            ivnl_end = ivnl - 1
            exit
          end if
        end do


        ik_loop: do ik = 1, st%d%nik
          st_loop: do ist = st%st_start, st%st_end

            do j = 1, NDIM
              do idim = 1, st%d%dim
                if (st%d%wfs_type == M_REAL) then
                  dz = dpsiprojectpsi(gr%m, ep%dp(j, ivnl_start:ivnl_end), &
                       ivnl_end - ivnl_start + 1, st%dpsi(:, idim, ist, ik), &
                       periodic = .false., ik = ik)
                  atm%f(j) = atm%f(j) + M_TWO * st%occ(ist, ik) * dz
                else
                  zz = zpsiprojectpsi(gr%m, ep%dp(j, ivnl_start:ivnl_end), &
                       ivnl_end - ivnl_start + 1, st%zpsi(:, idim, ist, ik), &
                       periodic = .false., ik = ik)
                  atm%f(j) = atm%f(j) + M_TWO * st%occ(ist, ik) * zz
                end if

              end do
            end do

          end do st_loop
        end do ik_loop

      end do atm_loop
      if (st%d%wfs_type == M_REAL) then
        deallocate(dppsi)
      else
        deallocate(zppsi)
      end if
    end if

#if defined(HAVE_MPI)
    do i = 1, geo%natoms
      atm => geo%atom(i)
      if(atm%spec%local) cycle

      if(st%parallel_in_states) then
        call MPI_Allreduce(atm%f(1), f(1), NDIM, MPI_FLOAT, MPI_SUM, st%mpi_grp%comm, mpi_err)
        atm%f = f
      end if
    end do
#endif

    if(.not.geo%only_user_def) then ! exclude user defined species for the moment
      ! Now the ion, ion force term
      do i = 1, geo%natoms
        zi = geo%atom(i)%spec%Z_val
        do j = 1, geo%natoms
          if(i .ne. j) then
            zj = geo%atom(j)%spec%Z_val
            r = sqrt(sum((geo%atom(i)%x(1:NDIM) - geo%atom(j)%x(1:NDIM))**2))
            d = zi * zj/r**3

            geo%atom(i)%f(1:NDIM) = geo%atom(i)%f(1:NDIM) + &
                 d*(geo%atom(i)%x(1:NDIM) - geo%atom(j)%x(1:NDIM))
          end if
        end do
      end do
    end if

    ! now comes the local part of the PP
    if(.not.simul_box_is_periodic(gr%sb).or.geo%only_user_def) then ! Real space
      call local_RS()
#if defined(HAVE_FFT)
    else ! Fourier space
      call local_FS()
#endif
    end if

    if(present(t).and.ep%no_lasers>0) then
      call laser_field(gr%sb, ep%no_lasers, ep%lasers, t, x)
      do i = 1, geo%natoms
        geo%atom(i)%f(1:NDIM) = geo%atom(i)%f(1:NDIM) + &
             geo%atom(i)%spec%Z_val * x(1:NDIM)
      end do
    end if

    if(associated(ep%E_field)) then
      do i = 1, geo%natoms
        geo%atom(i)%f(1:NDIM) = geo%atom(i)%f(1:NDIM) + &
             geo%atom(i)%spec%Z_val * ep%E_field(1:NDIM)
      end do
    end if

    !TODO: forces due to the magnetic fields (static and time-dependent)

    call pop_sub()
    call profiling_out(C_PROFILING_FORCES)

  contains

    ! ---------------------------------------------------------
    subroutine local_RS()
      FLOAT :: r
      FLOAT, allocatable :: force(:,:), x(:), gv(:)
      integer  :: i, j, k, ns

      ALLOCATE(x(MAX_DIM), MAX_DIM)
      ALLOCATE(gv(MAX_DIM), MAX_DIM)
      ns = min(2, st%d%nspin)

      ALLOCATE(force(NP, MAX_DIM), NP*MAX_DIM)
      do i = 1, geo%natoms
        atm => geo%atom(i)

        do j = 1, NP
          call mesh_r(gr%m, j, r, x=x, a=atm%x)
          call specie_get_glocal(atm%spec, x, gv)
          force(j,1:NDIM) = sum(st%rho(j, 1:ns))*gv(1:NDIM)
        end do

        do k = 1, NDIM
          atm%f(k) = atm%f(k) - dmf_integrate(gr%m, force(:, k))
        end do
      end do

      deallocate(force, x, gv)
    end subroutine local_RS


#ifdef HAVE_FFT
    ! ---------------------------------------------------------
    subroutine local_FS()
      type(dcf_t) :: cf_for
      FLOAT, allocatable :: force(:)
      
      ALLOCATE(force(NP), NP)
      call dcf_new_from(cf_for, ep%local_cf(1)) ! at least one specie must exist
      call dcf_alloc_FS(cf_for)
      call dcf_alloc_RS(cf_for)

      do i = 1, geo%natoms
        atm => geo%atom(i)
        do j = 1, NDIM
          cf_for%FS = M_z0
          call cf_phase_factor(gr%sb, gr%m, atm%x, ep%local_cf(atm%spec%index), cf_for)

          call dcf_FS_grad(gr%sb, gr%m, cf_for, j)
          call dcf_FS2RS(cf_for)
          call dcf2mf(gr%m, cf_for, force)
          do l = 1, st%d%nspin
            ! FIXME: When running with partitions, vol_pp is local
            ! to the node. It is likely, that this code need changes.
            atm%f(j) = atm%f(j) + sum(force(1:NP)*st%rho(1:NP, l)*gr%m%vol_pp(1:NP))
          end do
        end do
      end do

      call dcf_free(cf_for)
      deallocate(force)
    end subroutine local_FS
#endif

  end subroutine epot_forces


#include "undef.F90"
#include "real.F90"
#include "epot_inc.F90"

#include "undef.F90"
#include "complex.F90"
#include "epot_inc.F90"

end module external_pot_m


