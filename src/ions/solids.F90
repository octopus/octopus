!! Copyright (C) 2007 X. Andrade, M. Marques
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id: simul_box.F90 3479 2007-11-09 08:36:10Z xavier $

#include "global.h"

module solids_m
  use geometry_m
  use global_m
  use io_m
  use messages_m
  use simul_box_m
  use units_m

  implicit none

  private

  public ::                   &
    periodic_copy_t,          &
    periodic_copy_init,       &
    periodic_copy_end,        &
    periodic_copy_position,   &
    periodic_copy_num,        &
    periodic_write_crystal

  type periodic_copy_t
    private
    FLOAT :: pos(1:MAX_DIM)
    FLOAT :: pos_chi(1:MAX_DIM)
    FLOAT :: range
    integer :: nbmax(MAX_DIM), nbmin(MAX_DIM)
  end type periodic_copy_t

contains

  subroutine periodic_copy_init(this, sb, pos, range)
    type(periodic_copy_t), intent(out) :: this
    type(simul_box_t),     intent(in)  :: sb
    FLOAT,                 intent(in)  :: pos(1:MAX_DIM)
    FLOAT,                 intent(in)  :: range

    integer :: pd
    
    this%range = range
    this%pos = pos

    if(.not. simul_box_is_periodic(sb)) then
      this%nbmin = 0
      this%nbmax = 0
      return
    end if

    pd = sb%periodic_dim

    !convert the position to the orthogonal space
    this%pos_chi(1:pd) = matmul(pos(1:pd), sb%klattice_unitary(1:pd, 1:pd))

    this%nbmin(1:pd) = int((this%pos_chi(1:pd) - range)/sb%lsize(1:pd) - M_HALF)
    this%nbmax(1:pd) = int((this%pos_chi(1:pd) + range)/sb%lsize(1:pd) + M_HALF)

    !there are no copies in non-periodic directions
    this%nbmin(pd + 1:MAX_DIM) = 0
    this%nbmax(pd + 1:MAX_DIM) = 0

  end subroutine periodic_copy_init

  subroutine periodic_copy_end(this)
    type(periodic_copy_t), intent(inout) :: this

    this%nbmin = 0
    this%nbmax = 0

  end subroutine periodic_copy_end

  integer pure function periodic_copy_num(this) result(num)
    type(periodic_copy_t), intent(in)    :: this

      num = product(this%nbmax - this%nbmin + 1)

  end function periodic_copy_num

  function periodic_copy_position(this, sb, ii) result(pcopy)
    type(periodic_copy_t),   intent(in)  :: this
    type(simul_box_t),       intent(in)  :: sb
    integer, intent(in)                  :: ii
    FLOAT                                :: pcopy(MAX_DIM) 
    
    integer :: icell1, icell2, icell3, jj
    integer :: pd

    pd = sb%periodic_dim

    if(.not. simul_box_is_periodic(sb)) then
      pcopy = this%pos
      return
    end if

    jj = 1
    do icell1 = this%nbmin(1), this%nbmax(1)
      do icell2 = this%nbmin(2), this%nbmax(2)
        do icell3 = this%nbmin(3), this%nbmax(3)
          
          if (jj == ii) then 
            pcopy(1:pd) = this%pos_chi(1:pd) - M_TWO*sb%lsize(1:pd)*(/icell1, icell2, icell3/)
            pcopy(1:pd) = matmul(sb%rlattice(1:pd, 1:pd), pcopy(1:pd))
            pcopy(pd + 1:MAX_DIM) = this%pos(pd+1:MAX_DIM)
            return
          end if
          jj = jj + 1

        end do
      end do
    end do

  end function periodic_copy_position

  ! This subroutine creates a crystal by replicating the geometry and
  ! writes the result to dir//'crystal.xyz'
  subroutine periodic_write_crystal(sb, geo, dir)
    type(simul_box_t), intent(in) :: sb
    type(geometry_t),  intent(in) :: geo 
    character(len=*),  intent(in) :: dir
    
    type(periodic_copy_t) :: pp
    FLOAT :: radius
    integer :: total_atoms, iatom, icopy, iunit

    call push_sub('solids.periodic_write_crystal')
    
    radius = maxval(sb%lsize)*(M_ONE + M_EPSILON)

    !count the number of atoms in the crystal
    total_atoms = 0
    do iatom = 1, geo%natoms
      call periodic_copy_init(pp, sb, geo%atom(iatom)%x, radius)
      total_atoms = total_atoms + periodic_copy_num(pp)
      call periodic_copy_end(pp)
    end do
    
    ! now calculate
    iunit = io_open(trim(dir)//'/crystal.xyz', action='write')
    
    write(iunit, '(i9)') total_atoms
    write(iunit, '(a)') '#generated by Octopus'

    do iatom = 1, geo%natoms
      call periodic_copy_init(pp, sb, geo%atom(iatom)%x, radius)
      do icopy = 1, periodic_copy_num(pp)
        write(iunit, '(a, 3f12.6)') geo%atom(iatom)%label, &
          periodic_copy_position(pp, sb, icopy)/units_out%length%factor
      end do
      call periodic_copy_end(pp)
    end do

    call io_close(iunit)
    
    call pop_sub()
  end subroutine periodic_write_crystal

end module solids_m

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
