!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$

#include "global.h"

module mesh_m
  use checksum_m
  use curvilinear_m
  use datasets_m
  use geometry_m
  use global_m
  use hypercube_m
  use index_m
  use io_m
  use io_binary_m
  use math_m
  use mesh_cube_map_m
  use messages_m
  use mpi_m
  use multicomm_m
  use loct_m
  use par_vec_m
  use parser_m
  use profiling_m
  use simul_box_m
  use species_m
  use unit_m
  use unit_system_m

  implicit none
  
  private
  public ::                    &
    mesh_t,                    &
    mesh_plane_t,              &
    mesh_line_t,               &
    mesh_init_from_file,       &
    mesh_lxyz_init_from_file,  &
    mesh_dump,                 &
    mesh_end,                  &
    mesh_double_box,           &
    mesh_inborder,             &
    mesh_r,                    &
    mesh_gcutoff,              &
    mesh_write_info,           &
    mesh_nearest_point,        &
    mesh_subset_indices,       &
    mesh_periodic_point,       &
    mesh_global_memory,        &
    mesh_local_memory,         &
    mesh_x_global,             &
    mesh_write_fingerprint,    &
    mesh_read_fingerprint,     &
    mesh_compact_boundaries,   &
    translate_point

  !> Describes mesh distribution to nodes.
  !!
  !! Some general things:
  !! All members of type(mesh_t) are equal on all
  !! nodes when running parallel except
  !! - np, np_part
  !! - x, vol_pp
  !! These four are defined for all the points the node is responsible for.
  type mesh_t
    type(simul_box_t),   pointer :: sb  !< simulation box
    type(curvilinear_t), pointer :: cv  !< DOCUMENTATION NEEDED
    type(index_t)                :: idx !< DOCUMENTATION NEEDED
    logical :: use_curvilinear
    
    FLOAT :: spacing(MAX_DIM)         !< the (constant) spacing between the points
    
    ! When running serially, the local number of points is
    ! equal to the global number of points.
    ! Otherwise, the next two are different on each node.
    integer  :: np               !< Local number of points in mesh
    integer  :: np_part          !< Local points plus ghost points plus boundary points.
    integer  :: np_global        !< Global number of points in mesh.
    integer  :: np_part_global   !< Global number of inner points and boundary points.
    
    logical         :: parallel_in_domains !< will I run parallel in domains?
    type(mpi_grp_t) :: mpi_grp             !< the mpi group describing parallelization in domains
    type(pv_t)      :: vp                  !< describes parallel vectors defined on the mesh.
    
    FLOAT, pointer :: x(:,:)            !< The (local) points,
    integer, pointer :: resolution(:, :, :)
    FLOAT            :: volume_element    !< The global volume element.
    FLOAT, pointer   :: vol_pp(:)         !< Element of volume for curvilinear coordinates.
    type(mesh_cube_map_t) :: cube_map
  end type mesh_t
  
  !> This data type defines a plane, and a regular grid defined on 
  !! this plane (or, rather, on a portion of this plane)
  !! n should be a unit vector, that determines the normal of the plane.
  !! Origin is a point belonging to the plane
  !! u and v are unit orthogonal vectors belonging to the plane
  !! The grid is generated by the vectors u and v:
  !!   x_{i,j} = origin + i*spacing*u + j*spacing*v,
  !! for nu <= i <= mu and nv <= j <= mv
  type mesh_plane_t
    FLOAT :: n(MAX_DIM)
    FLOAT :: u(MAX_DIM), v(MAX_DIM)
    FLOAT :: origin(MAX_DIM)
    FLOAT :: spacing
    integer :: nu, mu, nv, mv
  end type mesh_plane_t
  
  !> This data type defines a line, and a regular grid defined on this
  !! line (or rather, on a portion of this line).
  type mesh_line_t
    FLOAT :: n(MAX_DIM)
    FLOAT :: u(MAX_DIM)
    FLOAT :: origin(MAX_DIM)
    FLOAT :: spacing
    integer :: nu, mu
  end type mesh_line_t
  
  integer, parameter, public ::      &
    LEFT_BOUNDARY_X   =  1,          &
    RIGHT_BOUNDARY_X  =  2,          &
    LEFT_BOUNDARY_Y   =  3,          &
    RIGHT_BOUNDARY_Y  =  4,          &
    LEFT_BOUNDARY_Z   =  5,          &
    RIGHT_BOUNDARY_Z  =  6,          &
    MAX_BOUNDARY_DIM  = RIGHT_BOUNDARY_Z
  
  character(len=17), parameter :: dump_tag = '*** mesh_dump ***'
  
contains

  ! ---------------------------------------------------------
  !> finds the dimension of a box doubled in the non-periodic dimensions
  subroutine mesh_double_box(sb, mesh, db)
    type(simul_box_t), intent(in)  :: sb
    type(mesh_t),      intent(in)  :: mesh
    integer,           intent(out) :: db(MAX_DIM)
    
    integer :: idir
    
    PUSH_SUB(mesh_double_box)

    db = 1
    
    ! double mesh with 2n points
    do idir = 1, sb%periodic_dim
      db(idir) = mesh%idx%ll(idir)
    end do
    do idir = sb%periodic_dim + 1, sb%dim
      db(idir) = nint(sb%fft_alpha * (mesh%idx%ll(idir) - 1)) + 1
    end do
    
    POP_SUB(mesh_double_box)
  end subroutine mesh_double_box
  
  
  ! ---------------------------------------------------------
  subroutine mesh_write_info(mesh, unit)
    type(mesh_t), intent(in) :: mesh
    integer,      intent(in) :: unit
    
    integer :: ii
    FLOAT :: cutoff

    if(.not.mpi_grp_is_root(mpi_world)) return
    
    PUSH_SUB(mesh_write_info)
    
    write(message(1),'(3a)') '  Spacing [', trim(units_abbrev(units_out%length)), '] = ('
    do ii = 1, mesh%sb%dim
      if(ii > 1) write(message(1), '(2a)') trim(message(1)), ','
      write(message(1), '(a,f6.3)') trim(message(1)), units_from_atomic(units_out%length, mesh%spacing(ii))
    end do
    write(message(1), '(5a,f8.5)') trim(message(1)), ') ', &
         '   volume/point [', trim(units_abbrev(units_out%length**mesh%sb%dim)), '] = ',      &
         units_from_atomic(units_out%length**mesh%sb%dim, mesh%vol_pp(1))
    
    write(message(2),'(a, i10)') '  # inner mesh = ', mesh%np_global
    write(message(3),'(a, i10)') '  # total mesh = ', mesh%np_part_global
    
    cutoff = mesh_gcutoff(mesh)**2 / M_TWO
    write(message(4),'(3a,f9.3,a,f9.3)') '  Grid Cutoff [', trim(units_abbrev(units_out%energy)),'] = ', &
      units_from_atomic(units_out%energy, cutoff), '    Grid Cutoff [Ry] = ', cutoff * 2
    call messages_info(4, unit)
    
    POP_SUB(mesh_write_info)
  end subroutine mesh_write_info
  
  
  ! ---------------------------------------------------------
  subroutine mesh_r(mesh, ip, rr, origin, coords)
    type(mesh_t), intent(in)  :: mesh
    integer,      intent(in)  :: ip
    FLOAT,        intent(out) :: rr
    FLOAT,        intent(in),  optional :: origin(:) ! origin(sb%dim)
    FLOAT,        intent(out), optional :: coords(:) ! coords(sb%dim)
   
    FLOAT :: xx(MAX_DIM)

    ! no push_sub because it is called too frequently
    
    xx(1:mesh%sb%dim) = mesh%x(ip, 1:mesh%sb%dim)
    if(present(origin)) xx(1:mesh%sb%dim) = xx(1:mesh%sb%dim) - origin(1:mesh%sb%dim)
    rr = sqrt(dot_product(xx(1:mesh%sb%dim), xx(1:mesh%sb%dim)))
    
    if(present(coords)) then
      coords(1:MAX_DIM) = M_ZERO
      coords(1:mesh%sb%dim) = xx(1:mesh%sb%dim)
    end if

  end subroutine mesh_r
  
  
  !---------------------------------------------------------------------
  !> Finds out if a given point of a mesh belongs to the "border" of the
  !! mesh. A point belongs to the border of the mesh if it is too close
  !! to any of the walls of the mesh. The criterion is set by input
  !! parameter "width".
  !!
  !! mesh  : the mesh.
  !! ip    : the point in the mesh.
  !! n     : on output, the number (0<=n<=3) of "walls" of the mesh that
  !!         the point is too close to, in order to consider it belonging
  !!         to a mesh.
  !! dist  : the distances of the point to the walls, for each of the walls
  !!         that the point is too close to.
  !! width : the width of the border.
  !!
  !! So, if n > 0, the point is in the border.
  ! ----------------------------------------------------------------------
  logical function mesh_inborder(mesh, geo, ip, dist, width) result(is_on_border)
    type(mesh_t),     intent(in)  :: mesh
    type(geometry_t), intent(in)  :: geo
    integer,          intent(in)  :: ip
    FLOAT,            intent(in)  :: width
    FLOAT,            intent(out) :: dist ! distance from border
    
    integer :: iatom, jatom, idir
    FLOAT   :: xx(MAX_DIM), rr, dd, radius
    
    ! no PUSH SUB, called too often

    is_on_border = .false.
    dist = M_ZERO

    select case(mesh%sb%box_shape)
    case(SPHERE)
      call mesh_r(mesh, ip, rr, coords=xx)
      dd = rr - (mesh%sb%rsize - width)
      if(dd.gt.M_ZERO) then
        is_on_border = .true.
        dist = dd
      end if

    case(CYLINDER)
      call mesh_r(mesh, ip, rr, coords=xx)
      dd = sqrt(xx(2)**2 + xx(3)**2) - (mesh%sb%rsize - width)
      if(dd.gt.M_ZERO) then
        is_on_border = .true.
        dist = dd
      end if
      if(mesh%sb%periodic_dim == 0) then
        dd = abs(xx(1)) - (mesh%sb%xsize - width)
        if(dd.gt.M_ZERO) then
          is_on_border = .true.
          dist = sqrt(dist*dist + dd*dd)
        end if
      end if

    case(MINIMUM)
      radius = mesh%sb%rsize
      do iatom = 1, geo%natoms
        call mesh_r(mesh, ip, rr, origin=geo%atom(iatom)%x, coords=xx)
        if(mesh%sb%rsize < M_ZERO) radius = species_def_rsize(geo%atom(iatom)%spec)
        dd = rr - (radius - width)
	! check if the point is on the spherical shell of atom # iatom
	if ((dd < M_ZERO) .or. (rr > radius)) cycle
 
        ! make sure that the point is not inside some other atomic sphere
        is_on_border = .true.
        do jatom = 1, geo%natoms
          if(jatom == iatom) cycle
          call mesh_r(mesh, ip, rr, origin=geo%atom(jatom)%x)
          if(mesh%sb%rsize < M_ZERO) radius = species_def_rsize(geo%atom(jatom)%spec)
          if(rr < radius - width) then 
            is_on_border = .false.
            exit
          end if
        end do

        if(is_on_border) dist = dd
      end do

    case(PARALLELEPIPED, HYPERCUBE)
      call mesh_r(mesh, ip, rr, coords=xx)
      do idir = mesh%sb%periodic_dim+1, mesh%sb%dim
        dd = abs(xx(idir)) - (mesh%sb%lsize(idir) - width)
        if(dd.gt.M_ZERO) then
          is_on_border = .true.
          dist = dist + dd*dd
        end if
      end do
      dist = sqrt(dist)

    case(BOX_IMAGE, BOX_USDEF)
      call messages_not_implemented('Absorbing boundaries for user-defined box')

    end select
    
    ! This may happen if the point is on more than one border at the same time.
    if(dist > width) dist = width

  end function mesh_inborder
  
  
  !---------------------------------------------------------------------
  !> Returns the index of the point which is nearest to a given vector
  !! position pos. Variable dmin will hold, on exit, the distance between
  !! pos and this nearest mesh point. rankmin will be zero, if the mesh is
  !! not partitioned, and the rank of the processor which holds the point
  !! ind if the mesh is partitioned.
  ! ----------------------------------------------------------------------
  integer function mesh_nearest_point(mesh, pos, dmin, rankmin) result(ind)
    type(mesh_t), intent(in)  :: mesh
    FLOAT,        intent(in)  :: pos(MAX_DIM)
    FLOAT,        intent(out) :: dmin
    integer,      intent(out) :: rankmin
    
    FLOAT :: dd
    integer :: imin, ip
#if defined(HAVE_MPI)
    FLOAT :: min_loc_in(2), min_loc_out(2)
#endif
    
    PUSH_SUB(mesh_nearest_point)
    
    !find the point of the grid that is closer to the atom
    dmin = M_ZERO
    do ip = 1, mesh%np
      dd = sum((pos(1:mesh%sb%dim) - mesh%x(ip, 1:mesh%sb%dim))**2)
      if((dd < dmin) .or. (ip == 1)) then 
        imin = ip
        dmin = dd
      end if
    end do
    
    rankmin = 0
#if defined(HAVE_MPI)
    if(mesh%parallel_in_domains) then
      min_loc_in(1) = dmin
      min_loc_in(2) = mesh%np_global * mesh%mpi_grp%rank  + real(imin, REAL_PRECISION) 
      call MPI_Allreduce(min_loc_in, min_loc_out, 1, MPI_FLOAT, &
        MPI_MINLOC, mesh%mpi_grp%comm, mpi_err)
      dmin = min_loc_out(1)
      imin = mod(nint(min_loc_out(2)), mesh%np_global)
      rankmin = nint(min_loc_out(2))/mesh%np_global
    end if
#endif
    
    ind = imin
    POP_SUB(mesh_nearest_point)
  end function mesh_nearest_point
  
  
  !--------------------------------------------------------------
  integer function translate_point(mesh, index, tdist) result (tindex)
    type(mesh_t), intent(in) :: mesh
    integer,      intent(in) :: index
    integer,      intent(in) :: tdist(:)
    
    integer :: ixyz(MAX_DIM), id
    
    PUSH_SUB(translate_point)
    
    ASSERT(index >= 1 .and. index <= mesh%np)
    
    ixyz(:) = mesh%idx%lxyz(index, :)
    
    do id = 1, mesh%sb%dim
      ixyz(id) = ixyz(id) + tdist(id)
    end do
    
    tindex = index_from_coords(mesh%idx, mesh%sb%dim, ixyz) 
    
    ! check if the translated point is still inside the domain and return
    ! a negative index otherwise to indicate that the requested translation
    ! is not valid
    if (tindex < 1 .or. tindex > mesh%np) then
      tindex = -1
    end if
    
    POP_SUB(translate_point)
  end function translate_point
  
  
  ! --------------------------------------------------------------
  !> mesh_gcutoff returns the "natural" band limitation of the
  !! grid mesh, in terms of the maximum G vector. For a cubic regular
  !! grid, it is M_PI/spacing.
  ! --------------------------------------------------------------
  FLOAT function mesh_gcutoff(mesh) result(gmax)
    type(mesh_t), intent(in) :: mesh

    PUSH_SUB(mesh_gcutoff)
    gmax = M_PI / (maxval(mesh%spacing))

    POP_SUB(mesh_gcutoff)
  end function mesh_gcutoff
  
  
  ! -------------------------------------------------------------- 
  subroutine mesh_dump(mesh, iunit)
    type(mesh_t), intent(in) :: mesh
    integer,      intent(in) :: iunit
    
    PUSH_SUB(mesh_dump)
    
    write(iunit, '(a)')         dump_tag
    write(iunit, '(a20,7i8)')   'nr(1, :)=           ', mesh%idx%nr(1, 1:mesh%sb%dim)
    write(iunit, '(a20,7i8)')   'nr(2, :)=           ', mesh%idx%nr(2, 1:mesh%sb%dim)
    write(iunit, '(a20,7i8)')   'l(:)=               ', mesh%idx%ll(1:mesh%sb%dim)
    write(iunit, '(a20,7i8)')   'enlarge(:)=         ', mesh%idx%enlarge(1:mesh%sb%dim)
    write(iunit, '(a20,1i10)')  'np=                 ', mesh%np
    write(iunit, '(a20,1i10)')  'np_part=            ', mesh%np_part
    write(iunit, '(a20,1i10)')  'np_global=          ', mesh%np_global
    write(iunit, '(a20,1i10)')  'np_part_global=     ', mesh%np_part_global
    
    POP_SUB(mesh_dump)
  end subroutine mesh_dump
  
  
  ! -------------------------------------------------------------- 
  !> Read the mesh parameters from file that were written by mesh_dump.
  subroutine mesh_init_from_file(mesh, iunit)
    type(mesh_t), intent(inout) :: mesh
    integer,      intent(in)    :: iunit

    character(len=20)  :: str
    character(len=100) :: line

    PUSH_SUB(mesh_init_from_file)

    ! Find (and throw away) the dump tag.
    do
      read(iunit, '(a)') line
      if(trim(line).eq.dump_tag) exit
    end do

    ASSERT(mesh%sb%dim > 0 .and. mesh%sb%dim <= MAX_DIM)

    mesh%idx%nr = 0
    mesh%idx%ll = 0
    mesh%idx%enlarge = 0
    read(iunit, '(a20,7i8)')  str, mesh%idx%nr(1, 1:mesh%sb%dim)
    read(iunit, '(a20,7i8)')  str, mesh%idx%nr(2, 1:mesh%sb%dim)
    read(iunit, '(a20,7i8)')  str, mesh%idx%ll(1:mesh%sb%dim)
    ! ll should not be read but computed:
    mesh%idx%ll(:) = mesh%idx%nr(2, :) - mesh%idx%nr(1, :) + 1
    read(iunit, '(a20,7i8)')  str, mesh%idx%enlarge(1:mesh%sb%dim)
    read(iunit, '(a20,1i10)') str, mesh%np
    read(iunit, '(a20,1i10)') str, mesh%np_part
    read(iunit, '(a20,1i10)') str, mesh%np_global
    read(iunit, '(a20,1i10)') str, mesh%np_part_global
    nullify(mesh%idx%lxyz, mesh%idx%lxyz_inv, mesh%x, mesh%vol_pp, mesh%resolution)
    mesh%parallel_in_domains = .false.

    POP_SUB(mesh_init_from_file)
  end subroutine mesh_init_from_file


  ! --------------------------------------------------------------
  subroutine mesh_write_fingerprint(mesh, filename)
    type(mesh_t),     intent(in) :: mesh
    character(len=*), intent(in) :: filename

    integer :: iunit

    PUSH_SUB(mesh_write_fingerprint)

    if(mesh%sb%box_shape /= HYPERCUBE) then

      iunit = io_open(trim(filename), action='write', is_tmp=.true.)
      write(iunit, '(a20,i21)')  'np_part_global=     ', mesh%np_part_global
      write(iunit, '(a20,i21)')  'np_global=          ', mesh%np_global
      write(iunit, '(a20,i21)')  'algorithm=          ', 1
      write(iunit, '(a20,i21)')  'checksum=           ', mesh%idx%checksum
      call io_close(iunit)

    end if

    POP_SUB(mesh_write_fingerprint)
  end subroutine mesh_write_fingerprint

  ! -----------------------------------------------------------------------
  ! This function reads the fingerprint of a mesh written in
  ! filename. If the mesh are equal (same fingerprint) return values
  ! are 0, otherwise it returns the size of the mesh stored. If the
  ! fingerprint cannot be read, it returns -1.

  subroutine mesh_read_fingerprint(mesh, filename, read_np_part, read_np)
    type(mesh_t),     intent(in)  :: mesh
    character(len=*), intent(in)  :: filename
    integer,          intent(out) :: read_np_part
    integer,          intent(out) :: read_np

    character(len=20)  :: str
    integer :: iunit, algorithm
    integer(8) :: checksum

    PUSH_SUB(mesh_read_fingerprint)

    iunit = io_open(trim(filename), action='read', status='old', die=.false., is_tmp = .true.)

    if(iunit < 0) then
      read_np_part = -1
      read_np = -1
    else

      read(iunit, '(a20,i21)')  str, read_np_part
      read(iunit, '(a20,i21)')  str, read_np
      read(iunit, '(a20,i21)')  str, algorithm
      read(iunit, '(a20,i21)')  str, checksum
      
      call io_close(iunit)

      ASSERT(read_np_part >= read_np)
      
      if(read_np_part == mesh%np_part_global &
        .and. read_np == mesh%np_global &
        .and. algorithm == 1 &
        .and. checksum == mesh%idx%checksum) then
        read_np_part = 0
        read_np = 0
      end if

    endif

    POP_SUB(mesh_read_fingerprint)

  end subroutine mesh_read_fingerprint

  ! --------------------------------------------------------------
  !> Fill the lxyz and lxyz_inv arrays from a file
  subroutine mesh_lxyz_init_from_file(mesh, filename)
    type(mesh_t),     intent(inout) :: mesh
    character(len=*), intent(in) :: filename

    integer :: ip, idir, ix(MAX_DIM), ierr

    PUSH_SUB(mesh_lxyz_init_from_file)

    ASSERT(mesh%sb%dim > 0 .and. mesh%sb%dim <= MAX_DIM)

    ! FIXME 4D
    ASSERT(mesh%sb%box_shape /= HYPERCUBE)

    call io_binary_read(trim(filename)//'.obf', mesh%np_part*mesh%sb%dim, mesh%idx%lxyz, ierr)

    if(ierr > 0) then
      message(1) = "Error: failed to read file "//trim(filename)//'.obf'
      call messages_fatal(1)
    end if

    do ip = 1, mesh%np_part
      forall (idir = 1:mesh%sb%dim) ix(idir) = mesh%idx%lxyz(ip, idir)
      forall (idir = mesh%sb%dim + 1:MAX_DIM) ix(idir) = 0
      mesh%idx%lxyz_inv(ix(1), ix(2), ix(3)) = ip
    end do

    POP_SUB(mesh_lxyz_init_from_file)
  end subroutine mesh_lxyz_init_from_file


  ! --------------------------------------------------------------
  !> Extracts the point numbers of a rectangular subset spanned
  !! by the two corner points from and to.
  subroutine mesh_subset_indices(mesh, from, to, indices)
    type(mesh_t), intent(in)  :: mesh
    integer,      intent(in)  :: from(MAX_DIM)
    integer,      intent(in)  :: to(MAX_DIM)
    integer,      intent(out) :: indices(:)

    integer :: lb(MAX_DIM) !< Lower bound of indices.
    integer :: ub(MAX_DIM) !< Upper bound of indices.

    integer :: ix, iy, iz, ii

    PUSH_SUB(mesh_subset_indices)

    ! In debug mode, check for valid indices in from, to first.
    if(in_debug_mode) then
      if(.not.index_valid(mesh, from).or..not.index_valid(mesh, to)) then
        message(1) = 'Failed assertion:'
        message(2) = 'mesh.mesh_subset_indices has been passed points outside the box:'
        message(3) = ''
        write(message (4), '(a, i6, a, i6, a, i6, a)') &
          '  from = (', from(1), ', ', from(2), ', ', from(3), ')'
        write(message(5), '(a, i6, a, i6, a, i6, a)') & 
          '  to   = (', to(1), ', ', to(2), ', ', to(3), ')'
        call messages_fatal(5)
      end if
    end if

    lb = min(from, to)
    ub = max(from, to)

    ii = 1
    do ix = lb(1), ub(1)
      do iy = lb(2), ub(2)
        do iz = lb(3), ub(3)
          indices(ii) = mesh%idx%lxyz_inv(ix, iy, iz)
          ii         = ii + 1
        end do
      end do
    end do

    POP_SUB(mesh_subset_indices)
  end subroutine mesh_subset_indices


  ! --------------------------------------------------------------
  !> Checks if the (x, y, z) indices of point are valid, i.e.
  !! inside the dimensions of the simulation box.
  logical function index_valid(mesh, point)
    type(mesh_t), intent(in) :: mesh
    integer,      intent(in) :: point(MAX_DIM)

    integer :: idir
    logical :: valid

    PUSH_SUB(index_valid)

    valid = .true.
    do idir = 1, mesh%sb%dim
      if(point(idir) .lt. mesh%idx%nr(1, idir) .or. point(idir) .gt. mesh%idx%nr(2, idir)) then
        valid = .false.
      end if
    end do

    index_valid = valid

    POP_SUB(index_valid)
  end function index_valid


  ! --------------------------------------------------------------
  recursive subroutine mesh_end(mesh, is_lead)
    type(mesh_t), intent(inout)   :: mesh
    logical, intent(in), optional :: is_lead

    logical :: is_lead_
#ifdef HAVE_MPI
    integer :: ipart
#endif

    PUSH_SUB(mesh_end)

    call mesh_cube_map_end(mesh%cube_map)

    if (present(is_lead)) then
      is_lead_ = is_lead
    else
      is_lead_ = .false.
    end if

    if (.not. is_lead_) then
      if(mesh%idx%sb%box_shape == HYPERCUBE) call hypercube_end(mesh%idx%hypercube)
    end if

    SAFE_DEALLOCATE_P(mesh%resolution)
    SAFE_DEALLOCATE_P(mesh%idx%lxyz)
    SAFE_DEALLOCATE_P(mesh%idx%lxyz_inv)
    SAFE_DEALLOCATE_P(mesh%x)
    SAFE_DEALLOCATE_P(mesh%vol_pp)

    if(mesh%parallel_in_domains) then
#if defined(HAVE_MPI)
      call vec_end(mesh%vp)
#endif
    end if

    POP_SUB(mesh_end)
  end subroutine mesh_end

  
  ! This function returns the point inside the grid corresponding to
  ! a boundary point when PBCs are used. In case the point does not
  ! have a correspondence (i.e. other BCs are used in that direction),
  ! the same point is returned. Note that this function returns a
  ! global point number when parallelization in domains is used.
  ! ---------------------------------------------------------  
  integer function mesh_periodic_point(mesh, ip) result(ipp)
    type(mesh_t), intent(in)    :: mesh
    integer,      intent(in)    :: ip
    
    integer :: ix(MAX_DIM), nr(2, MAX_DIM), idim
    
    ! no push_sub, called too frequently

    ix = mesh%idx%lxyz(ip, :)
    nr(1, :) = mesh%idx%nr(1, :) + mesh%idx%enlarge(:)
    nr(2, :) = mesh%idx%nr(2, :) - mesh%idx%enlarge(:)
    
    do idim = 1, mesh%sb%periodic_dim
      if(ix(idim) < nr(1, idim)) ix(idim) = ix(idim) + mesh%idx%ll(idim)
      if(ix(idim) > nr(2, idim)) ix(idim) = ix(idim) - mesh%idx%ll(idim)
    end do
    
    ipp = mesh%idx%lxyz_inv(ix(1), ix(2), ix(3))
    
  end function mesh_periodic_point
  

  ! ---------------------------------------------------------
  real(8) pure function mesh_global_memory(mesh) result(memory)
    type(mesh_t), intent(in) :: mesh
    
    memory = 0.0_8
    
    ! lxyz_inv
    memory = memory + SIZEOF_UNSIGNED_INT * product(mesh%idx%nr(2, 1:mesh%sb%dim) - mesh%idx%nr(1, 1:mesh%sb%dim) + M_ONE)
    ! resolution
    if(mesh%sb%mr_flag) then
      memory = memory + SIZEOF_UNSIGNED_INT * product(mesh%idx%nr(2, 1:mesh%sb%dim) - mesh%idx%nr(1, 1:mesh%sb%dim) + M_ONE)
    end if
    ! lxyz
    memory = memory + SIZEOF_UNSIGNED_INT * dble(mesh%np_part_global) * MAX_DIM

  end function mesh_global_memory


  ! ---------------------------------------------------------
  real(8) pure function mesh_local_memory(mesh) result(memory)
    type(mesh_t), intent(in) :: mesh
    
    memory = 0.0_8
    
    ! x
    memory = memory + REAL_PRECISION * dble(mesh%np_part) * MAX_DIM
  end function mesh_local_memory


  ! ---------------------------------------------------------
  function mesh_x_global(mesh, ip, force) result(xx)
    type(mesh_t),       intent(in) :: mesh
    integer,            intent(in) :: ip
    logical, optional,  intent(in) :: force
    FLOAT                          :: xx(1:MAX_DIM)

    FLOAT :: chi(1:MAX_DIM)
    integer :: ix(1:MAX_DIM)
    logical :: force_

! no push_sub because function is called too frequently

    force_ = .false.
    if (present(force)) force_ = force
      
    if(mesh%parallel_in_domains .or. force_) then
      call index_to_coords(mesh%idx, mesh%sb%dim, ip, ix)
      chi(1:mesh%sb%dim) = ix(1:mesh%sb%dim) * mesh%spacing(1:mesh%sb%dim)
      chi(mesh%sb%dim + 1:MAX_DIM) = M_ZERO
      xx = M_ZERO ! this initialization is required by gfortran 4.4 or we get NaNs
      call curvilinear_chi2x(mesh%sb, mesh%cv, chi, xx)
      xx(mesh%sb%dim + 1:MAX_DIM) = M_ZERO
    else
      xx(1:MAX_DIM) = mesh%x(ip, 1:MAX_DIM)
    end if

  end function mesh_x_global


  ! ---------------------------------------------------------
  logical pure function mesh_compact_boundaries(mesh) result(cb)
    type(mesh_t),       intent(in) :: mesh
    
    cb = .not. mesh%use_curvilinear .and. &
         .not. mesh%parallel_in_domains .and.  &
         simul_box_has_zero_bc(mesh%sb)

  end function mesh_compact_boundaries

end module mesh_m


!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
